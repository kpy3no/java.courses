plugins {
    id 'org.liquibase.gradle' version '2.0.4'
}

apply plugin: 'base'
apply plugin: 'java'

dependencies {
    liquibaseRuntime 'org.postgresql:postgresql:42.0.0'
    liquibaseRuntime 'org.liquibase:liquibase-core:3.8.9'
    //Чтобы жидкий (liquibase) мог читать конфигурацию в формате yaml.
    liquibaseRuntime 'org.yaml:snakeyaml:1.26'
}


sourceSets {
    test {
        resources {
            srcDir('liquibase')
        }
    }
}

liquibase {
    activities {
        main {
            changeLogFile 'liquibase/db/changelog/db.changelog-master.yaml'
            url "jdbc:postgresql://$host:$port/$database"
            driver 'org.postgresql.Driver'
            username "$user"
            password "$pass"
            contexts "$contexts"
        }
    }
}

task liquibasePackage {
    group = 'liquibase'
    description = 'Packages changelog and jars required to run liquibase'

    doLast {
        copy {
            from 'liquibase'
            into "${buildDir}/distrib/liquibase"
        }
        copy {
            from(configurations.liquibaseRuntime) {
                include 'postgresql*.jar'
            }
            into "${buildDir}/distrib/liquibase"
            rename 'postgresql-(.*).jar', 'postgresql-jdbc-driver.jar'
        }
    }
}
assemble.dependsOn liquibasePackage